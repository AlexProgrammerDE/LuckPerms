plugins {
    alias(libs.plugins.shadow)
}

configurations {
    testJar
}

repositories {
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
}

dependencies {
    compileOnly 'com.destroystokyo.paper:paper-api:1.15.2-R0.1-SNAPSHOT'

    implementation project(':api')
    implementation project(':common:loader-utils')
}

processResources {
    filesMatching('plugin.yml') {
        expand 'pluginVersion': project.ext.fullVersion
    }
}

shadowJar {
    archiveFileName = "LuckPerms-Bukkit-${project.ext.fullVersion}.jar"

    from {
        project(':bukkit').tasks.shadowJar.archiveFile
    }
}

tasks.register('copyTestJar', Copy) {
    from tasks.shadowJar.archiveFile
    into layout.buildDirectory.dir('testJars')
    rename { String fileName ->
        return 'luckperms-bukkit.testjar'
    }
}

artifacts.add('testJar',  layout.buildDirectory.dir('testJars')) {
    builtBy('copyTestJar')
}

artifacts {
    archives shadowJar
}
